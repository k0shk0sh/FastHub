apply plugin: 'com.android.application'
if (isGms) {
    apply plugin: 'com.google.firebase.firebase-crash'
}

apply plugin: 'com.novoda.build-properties'
apply plugin: 'jacoco-android'

buildProperties {
    notThere {
        file rootProject.file('debug_gradle.properties')
    }
    secrets {
        file rootProject.file('gradle.properties')
    }
}

android {
    signingConfigs {
        signing {
            keyAlias((buildProperties.secrets['android_key_alias'] | buildProperties.notThere['android_key_alias']).string)
            keyPassword((buildProperties.secrets['android_store_password'] | buildProperties.notThere['android_store_password']).string)
            storeFile file('fastaccess-key')
            storePassword((buildProperties.secrets['android_store_password'] | buildProperties.notThere['android_store_password']).string)
        }
    }
    compileSdkVersion 'android-O'
    buildToolsVersion "26.0.0-rc2"
    defaultConfig {
        applicationId "com.fastaccess.github"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 210
        versionName "2.1.0"
        signingConfig signingConfigs.signing
        buildConfigString "GITHUB_CLIENT_ID", (buildProperties.secrets['github_client_id'] | buildProperties.notThere['github_client_id']).string
        buildConfigString "GITHUB_SECRET", (buildProperties.secrets['github_secret'] | buildProperties.notThere['github_secret']).string
        buildConfigString "IMGUR_CLIENT_ID", (buildProperties.secrets['imgur_client_id'] | buildProperties.notThere['imgur_client_id']).string
        buildConfigString "IMGUR_SECRET", (buildProperties.secrets['imgur_secret'] | buildProperties.notThere['imgur_secret']).string
        buildConfigField "String", "REST_URL", '"https://api.github.com/"'
        buildConfigField "String", "IMGUR_URL", '"https://api.imgur.com/3/"'
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "platform"

    productFlavors {
        // includes gms
        gms {
            buildConfigField "boolean", "FDROID", "false"
            dimension "platform"
        }

        // only foss
        fdroid {
            buildConfigField "boolean", "FDROID", "true"
            dimension "platform"
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    "src/main/res/",
                    "src/main/res/layouts/main_layouts",
                    "src/main/res/layouts/row_layouts",
                    "src/main/res/layouts/other_layouts",
                    "src/main/res/translations",
                    "src/main/res"
            ]
        }
    }

    lintOptions {
        abortOnError false
        htmlReport true
        xmlReport true
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations {
        all*.exclude module: 'annotations'
    }

    dexOptions {
        jumboMode true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    splits{
        density{
            enable true
        }
    }
}

repositories {
    maven { url "https://clojars.org/repo/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"
    implementation "com.android.support:cardview-v7:${supportVersion}"
    implementation "com.android.support:recyclerview-v7:${supportVersion}"
    implementation "com.android.support:preference-v14:${supportVersion}"
    implementation "com.android.support:customtabs:${supportVersion}"
    implementation "com.android.support:palette-v7:${supportVersion}"
    implementation "net.grandcentrix.thirtyinch:thirtyinch:${thirtyinchVersion}"
    implementation "net.grandcentrix.thirtyinch:thirtyinch-rx:${thirtyinchVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'cn.gavinliu.android.lib:ShapedImageView:0.8.3'
    implementation "frankiesardo:icepick:${icepickVersion}"
    implementation "com.jakewharton:butterknife:${butterKnifeVersion}"
    implementation 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.1-rc1'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'com.annimon:stream:1.1.7'
    implementation 'com.github.GrenderG:Toasty:1.1.3'
    implementation 'com.github.JediBurrell:MaterialTapTargetPrompt:-SNAPSHOT'
    implementation 'com.github.k0shk0sh:RetainedDateTimePickers:1.0.2'
    implementation 'com.github.daniel-stoneuk:material-about-library:1.8.1'
    implementation 'io.requery:requery:1.3.2'
    implementation 'io.requery:requery-android:1.3.2'
    implementation('com.mikepenz:aboutlibraries:5.9.5@aar') { transitive = true }
    implementation 'com.github.nightwhistler:HtmlSpanner:0.4'
    implementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.2'
    implementation 'com.github.matthiasrobbers:shortbread:1.0.1'
    implementation 'com.atlassian.commonmark:commonmark:0.9.0'
//    implementation "com.google.android.gms:play-services-auth:10.2.6"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'io.requery:requery-processor:1.3.2'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "frankiesardo:icepick-processor:${icepickVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    annotationProcessor 'com.github.matthiasrobbers:shortbread-compiler:1.0.1'
    if (isGms) {
        implementation "com.google.firebase:firebase-ads:${firebase}"
        implementation "com.google.firebase:firebase-messaging:${firebase}"
        releaseImplementation "com.google.firebase:firebase-crash:${firebase}"
    }
    testImplementation "net.grandcentrix.thirtyinch:thirtyinch-test:$thirtyinchVersion"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    androidTestImplementation "com.android.support:support-annotations:${supportVersion}"
    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:${espresseVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espresseVersion}"
}

if (isGms) {
    apply plugin: 'com.google.gms.google-services'
}